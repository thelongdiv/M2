
doc ///
	Key
		"First LieAlgebra Tutorial"
	Headline 
		A tutorial of the package GradedLieAlgebras
	Description
		Text
	       	        In this elementary tutorial, we give a brief introduction 
			on how to use the package 
			GradedLieAlgebras.	
					    	    	
		Text
		    	The most common way to construct a Lie algebra 
			is by means of the constructor @TO lieAlgebra@, which
			produces a free Lie algebra on the generators given
			in input.
		
			
		Example
		        L = lieAlgebra({a,b})
			dimsLie 5
			
		Text
		        The above list is the dimensions in degrees 1 to 5 of the 
			free Lie algebra on two generators.
			To get an explicit basis in a certain degree,
			use 
			 @TO basisLie@. 
			
		Example
			basisLie 2
			basisLie 3
			
		Text
		        The basis elements in degree 3 above should be interpreted as 
			[a,[b,a]] and [b,[b,a]]. To multiply two Lie elements, use @TO multLie@
			or @TO (symbol SPACE,LieElement,LieElement)@. The operator SPACE is right 
			associative, so writing (a a a b) as input gives the Lie monomial [a,[a,[a,b]]],
			which in output is written in the same way as input. A linear combination of Lie monomials
			is written in the natural way.  
			
		Example
			p = (a b) (a a b + 3 b b a) 
							
		Text
			The output is a linear 
			combination of the basis elements
			of degree 5.
			
		Example 
		    	basisLie 5
			
		Text				
		        The element p in L may be used to define 
			a quotient Lie algebra by the ideal generated by p.
			
				
		Example
		        Q = L/{p}	
		        dimsLie 5
			
		Text
		        As expected, the dimension in degree 5 of Q is one less than 
			that of L.
		 
		Text
		        When L is a big free Lie algebra it may be better to 
			define the relations in a "formal" manner. For an example,
			see @TO "Constructing Lie algebras"@.
			
		Text
		        A generator for a Lie algebra may be any variable
			name, including indexed variables. Also, the same
			names can be used in different Lie algebras or 
			rings. Use @TO useLie@ and @TO use@ to switch between
			Lie algebras and rings.
			
	        Example
		        L = lieAlgebra{a,b}
			M = lieAlgebra{a,b}/{a b}
			R = QQ[a,b]
			useLie L
			a b
			useLie M
			a b
			use R
			a*b
		      
						
	SeeAlso		    		        
		 "Second LieAlgebra Tutorial"
		 "Differential LieAlgebra Tutorial"	      
		 "Constructing Lie algebras"
		 "Holonomy Lie algebras and Symmetries"
				       	 
///
end			
	
	
	